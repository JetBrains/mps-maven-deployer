plugins {
    id 'base'
    id 'maven-publish'
    id 'de.undercouch.download' version '3.1.1'
}
repositories {
    mavenCentral()
    maven {
        url 'http://localhost:8081/artifactory/local-main/'
        credentials {
            username = "username"
            password = "password"
        }
    }
}
configurations {
    mpsLang {transitive = false}
}
dependencies {
    mpsLang group: mpsLangGroupId, name: mpsLangArtifactId, version: mpsLangVersion, ext: mpsLangPackaging
}

// mps-driver depends on these libraries for compilation
def MPS_LIBS = ['mps-core', 'mps-openapi', 'mps-tool']

initMpsHomeAndVersion()

group 'org.jetbrains.mps'
version mpsVersion

task downloadMps(type: de.undercouch.gradle.tasks.download.Download) {
    src "http://download.jetbrains.com/mps/${mpsMajor}/MPS-${mpsVersion}.zip"
    dest "$buildDir/downloads/MPS-${mpsVersion}.zip"
    description "Downloads MPS ${mpsVersion}"
    overwrite false
}

task unpackMps(dependsOn: downloadMps) {
    description "Extracts downloaded MPS to $mpsHome"
    inputs.file downloadMps
    outputs.dir mpsHome

    doLast {
        copy {
            from zipTree(downloadMps.dest)
            from zipTree(configurations.mpsLang.singleFile)
            into temporaryDir
        }
        delete mpsHome
        copy {
            println "mpsLangArtifactId = $mpsLangArtifactId"
            from new File(temporaryDir, "MPS $mpsMajor")
            from new File(temporaryDir, mpsLangArtifactId)
            into mpsHome
        }
    }
}

task repackageMps(type: Zip, dependsOn: unpackMps) {
    baseName 'mps'
    from unpackMps
}

task packageMpsLibsAsSingleJar(type: Jar, dependsOn: unpackMps) {
    baseName 'mps-libs'

    MPS_LIBS.each {
        from zipTree("${mpsHome}/lib/${it}.jar")
    }
}

publishing {
    publications {
        mps(MavenPublication) {
            artifact source: repackageMps
            artifactId 'mps'
        }
        mpsLibs(MavenPublication) {
            artifact source: packageMpsLibsAsSingleJar
            artifact source: "$mpsHome/lib/MPS-src.zip", classifier: 'sources', extension: 'jar'
            artifactId 'mps-libs'
        }
    }

    if (project.hasProperty('repositoryUrl')) {
        repositories {
            maven {
                url project.repositoryUrl
                if (project.hasProperty('repositoryUsername')) {
                    credentials {
                        username = project.repositoryUsername
                        password = project.repositoryPassword
                    }
                }
            }
        }
    }
}

private void initMpsHomeAndVersion() {
    if (!hasProperty('mpsVersion')) {
        throw new GradleException("mpsVersion property must be defined")
    }

    ext.mpsMajor = mpsVersion.find(/^\d+\.\d+/)
    ext.mpsHome = "$buildDir/MPS-$mpsVersion"
}
